set dotenv-load

default:
    @just --list


# Aliases 
alias s := serve
alias m := migrate
alias b := build
alias t := test
alias mn := migrate-new
alias sw := swagger

# Serve the application
serve:
    go run . serve


# Run migrations 
migrate:
    go run . migrate up


# Build the application
build name = "main": test
    go build -o bin/{{name}} .

# Run tests
test:
    go test ./...

# Create new migration
migrate-new name:
    go run . migrate new {{name}}

# Reset the database by stopping containers, recreating them, and running migrations
reset:
    docker compose down
    docker compose up -d
    sleep 1
    @just migrate

# Generate swagger docs or update them (you need to have gog installed)
swagger:
    swag init -g cmd/serve/serve.go