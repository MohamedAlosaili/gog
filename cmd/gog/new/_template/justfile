set dotenv-load

default:
    @just --list


# Aliases 
alias s := serve
alias m := migrate
alias b := build
alias t := test
alias mn := migrate-new
alias sw := swagger
alias db := docker-build
alias dr := docker-run
alias dbr := docker-build-run
alias gc := generate-creds
alias rc := regenerate-creds

# Serve the application
serve:
    go run . serve -c config.yaml


# Run migrations 
migrate:
    go run . migrate up -c config.yaml


# Build the application
build name = "main": test
    go build -o bin/{{name}} .

# Run tests
test:
    go test ./...

# Create new migration
migrate-new name:
    go run . migrate new {{name}} -c config.yaml

# Reset the database by stopping containers, recreating them, and running migrations
reset:
    docker compose down
    docker compose up -d
    sleep 1
    @just migrate

# Generate swagger docs or update them (you need to have gog installed)
swagger:
    gog swag init -g cmd/serve/serve.go

docker-build:
    docker build -t PROJECT_NAME-image:latest -f devops/Dockerfile .

docker-run:
    docker run --rm --name PROJECT_NAME PROJECT_NAME-image:latest

docker-build-run: docker-build docker-run

generate-creds:
    nsc generate creds --name PROJECT_NAME_user --account PROJECT_NAME_account --output-file secrets/PROJECT_NAME_user.creds

regenerate-creds:
    rm secrets/PROJECT_NAME_user.creds
    nsc generate creds --name PROJECT_NAME_user --account PROJECT_NAME_account --output-file secrets/PROJECT_NAME_user.creds

migrate-down:
    go run . migrate down -c config.yaml